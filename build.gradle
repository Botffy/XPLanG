apply plugin: 'application'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'

version = 'git describe --tags --always'.execute().text.trim()
sourceCompatibility = 1.8
mainClassName = "ppke.itk.xplang.ui.Main"

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.github.stefanbirkner', name: 'fishbowl', version: '1.4.0'
    compile group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.7.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    runtime group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.21'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

checkstyle {
    toolVersion = '7.3'
    sourceSets = [sourceSets.main]
    showViolations = true
    ignoreFailures = true
}

pmd {
    ignoreFailures = true
    ruleSets = [
        "config/pmd/pmd.xml"
    ]
}

checkstyleTest {
    enabled = false
}

tasks.withType(Checkstyle) {
    reports {
        html.destination rootProject.file("build/reports/checkstyle.html")
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
    destinationDir = file('docs/javadoc')
    options.noQualifiers = ["java", "com.sun"]
    options.links = ['http://docs.oracle.com/javase/8/docs/api/']
    options.memberLevel = JavadocMemberLevel.PACKAGE;
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
    options.docEncoding = 'UTF-8'
}

import org.apache.tools.ant.filters.ReplaceTokens
import org.apache.tools.ant.filters.EscapeUnicode
tasks.withType(ProcessResources).each { task ->
    task.from(task.getSource()) {
        include '**/*.properties'
        exclude '**/language/**'
        filter(EscapeUnicode)
        filter(ReplaceTokens, tokens: [
            "version": version,
            "built.at": (new Date()).format("yyyy-MM-dd HH:mm:ssZ"),
            "built.by": System.getProperty('user.name'),
            "build.JDK": System.getProperty('java.version')
        ])
    }
}

run {
    if(project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
    main = "ppke.itk.xplang.ui.Main"
    classpath = sourceSets.main.runtimeClasspath
}
